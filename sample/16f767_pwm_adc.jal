-- -----------------------------------------------------------------------------
-- Title: Test program for testing a combination of PWM and ADC (2 channels both)
--
-- Author: Rob Hamerling, Copyright (c) 2009..2021, all rights reserved.
--
-- Adapted-by: Rob Jansen
--
-- Compiler: 2.5r5
-- Revision: $Revision$
--
-- This file is part of jallib (https://github.com/jallib/jallib)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description:
-- Using a combination of 2 ADC and PWM channels.
-- Two potmeters determine the luminosity of 2 LEDs.
-- Two different PWM frequencies are set, both allowing only low
-- resolution PWM dutycycle setting.
--
-- This file has been generated by hand (manually maintained)
--
-- Sources:
--
-- Notes:
--
-- -----------------------------------------------------------------------------
--

include 16f767

enable_digital_io()                             -- disable analog I/O (if any)

-- Internal oscillator at 4 MHz
pragma target clock       4_000_000             -- oscillator frequency

-- Configuration bits:
pragma target OSC      INTOSC_NOCLKOUT          -- internal, no clock out
pragma target WDT      disabled                 -- no watchdog
pragma target PWRTE    disabled                 -- startup delay
pragma target MCLR     external                 -- reset externally
pragma target BROWNOUT enabled                  -- brown out
pragma target VOLTAGE  V42                      -- 4.2 Volt brownout
pragma target DEBUG    disabled                 -- not needed
pragma target CCP2MUX  pin_C1                   -- CCP2 output on pin_C1
pragma target CP       disabled                 -- no code protection
pragma target FCMEN    disabled                 -- no fail safe clock monitoring
pragma target IESO     disabled                 -- no clock switch over
pragma target BORSEN   disabled                 -- no software bor
--
OSCCON_IRCF = 0b110                             -- 4 MHz
OSCCON_SCS  = 0b00                              -- oscillator determined by config bits

-- Include the serial library for testing purposes.
include print
const serial_hw_baudrate = 19200
include serial_hardware
serial_hw_init()

-- -------- ADC setup -----------
const ADC_CHANNEL_A = 11                        -- AN11 (pin_B4)
const ADC_CHANNEL_B = 13                        -- AN13 (pin_B5)
-- Step 1: ADC input pin setup 
pin_AN11_direction = input                      
pin_AN13_direction = input                      
ADCON1_PCFG = 0b0000                            -- Pins are analog
-- Step 2: Set VDD and VSS as Vref
ADCON1_VCFG0 = FALSE                            -- Vref+ is VDD
ADCON1_VCFG1 = FALSE                            -- Vref- is VSS
-- Step 3: Use Frc as ADC clock 
ADCON1_ADCS2 = FALSE 
ADCON0_ADCS = 0b011 
-- The maximum resistance while measuring ADC is... (unit: ohms)
-- Being accurate helps speeding up ADC acquisition
const word ADC_RSOURCE = 5_000
-- Now we can include the library
include adc
-- And initialize the whole with our parameters
adc_init()

-- ---- PWM setup set PWM-pins output! -------
pin_CCP1_direction     = output                 -- ) Pin 13 for 28 pin PDIP
pin_CCP2_RC1_direction = output                 -- ) Pin 12 for 28 pin PDIP
include pwm_hardware

var byte measure_a                              -- ADC channel A
var byte measure_b                              -- ADC channel B

pwm_set_frequency(10000)                        -- PWM frequency

forever loop

   measure_a = adc_read_low_res(ADC_CHANNEL_A)  -- read potmeter 1
   measure_b = adc_read_low_res(ADC_CHANNEL_B)  -- read potmeter 2

   -- Print the measured values (for testing purposes only)
   print_byte_dec(serial_hw_data, measure_a)
   print_string(serial_hw_data, " ")
   print_byte_dec(serial_hw_data, measure_b)
   print_crlf(serial_hw_data)

   pwm1_set_dutycycle_ratio(4 * measure_a)
   pwm2_set_dutycycle_ratio(4 * measure_b)

end loop

