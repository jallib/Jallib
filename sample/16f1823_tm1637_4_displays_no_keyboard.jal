-- -----------------------------------------------------------------------------
-- Title: Test program for the TM1637 with four 7-segment displays.
-- Author: Rob Jansen, Copyright (c) 2020..2022 all rights reserved.
-- Adapted-by:
-- Compiler: 2.5r6
--
-- This file is part of jallib (https://github.com/jallib/jallib)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: Shows the features of the TM1637 library for the display module.
--              No keyboard functionality is available. Test is for 4 displays.
--                 
-- Sources: Data sheet TM1637 from Titan Micro Electronics, V2.4.
--
-- Notes:   The following pins must be defined by the main program before 
--          including the library:
--          -) alias tm1637_clk            -- To CLK of the TM1637
--          -) alias tm1637_clk_direction 
--          -) alias tm1637_dio            -- To DIO of the TM1637 
--          -) alias tm1637_dio_direction 
--          Note that modules with 7-segement displays often have capacitors on
--          the clk and dio line. These should be removed.
--
--          The default number of displays is 6 which can be overruled by 
--          defining the constant TM1637_WIDTH with a value of 1 .. 6.
--          Depending on the module used, the display order can be different.
--          This can be set by defining an array TM1637_DISPLAY_ORDER where
--          the rightmost display has number 0 and the leftmost display has
--          number TM1637_WIDTH - 1.
--
--
include 16f1823                 

-- This program uses the internal oscillator at 32 MHz.
pragma target clock    32_000_000      -- oscillator frequency
pragma target OSC      INTOSC_NOCLKOUT -- Internal Clock
pragma target PLLEN    ENABLED         -- PLL on to get 32 MHz
pragma target WDT      DISABLED        -- No Watchdog
pragma target PWRTE    ENABLED         -- Power up timer enabled
pragma target BROWNOUT DISABLED        -- No brownout reset
pragma target FCMEN    DISABLED        -- No clock monitoring
pragma target IESO     DISABLED        -- int/ext osc. switch
pragma target LVP      ENABLED         -- Low voltage programming
pragma target MCLR     EXTERNAL        -- Reset external
-- Set the internal clock frequency to 32 MHz.
OSCCON_IRCF   = 0b1110                 -- Set 32 MHz (uses 8 MHz source)
OSCCON_SCS    = 0b00                   -- Clock determined by FOSC (32 MHz)

-- Enable weak pull up for all ports. 
WPUA = 0b0011_1111       
WPUC = 0b0011_1111       
OPTION_REG_WPUEN = FALSE 

enable_digital_io()

-- TM1637 pin definition.
alias tm1637_clk is pin_C0 -- Pin 10 for 14 pin DIP
alias tm1637_clk_direction is pin_C0_direction
alias tm1637_dio is pin_C1 -- Pin 9 for 14 pin DIP.    
alias tm1637_dio_direction is pin_C1_direction   

-- You can change the display width (default is 6 when not defined).
const byte TM1637_WIDTH = 4
-- Set the display order, rightmost is 0, leftmost is TM1637_WIDTH - 1.
const byte TM1637_DISPLAY_ORDER[TM1637_WIDTH] = {3,2,1,0} 

include tm1637
tm1637_init() 

--- Display 'hELLo ' on the display.
const byte DISPLAY_HELLO[TM1637_WIDTH] = 
             {
             0b0011_0011, -- R
             0b0011_1110, -- U
             0b0011_1111, -- O
             0b0111_0001  -- F
             }

-- Wait for some time. 
procedure wait_long() is
   _usec_delay(5_000_000)
end procedure


procedure wait_short() is
   _usec_delay(500_000)
end procedure
        
var sdword big_pos_counter
var sdword big_neg_counter
var word word_counter
var byte counter


forever loop

   -- Write a 'Four' message by writing the segment data directly into the
   -- global display data buffer.
   for TM1637_WIDTH using counter loop
      tm1637_display_data[counter] = DISPLAY_HELLO[counter]
   end loop
   -- Because we are using the global display data buffer we need to update 
   -- the display with this segment data.
   tm1637_display_update() 
   wait_long()
   
   -- Switch display off for some time
   tm1637_display_off()
   wait_short()
   tm1637_display_on()
         
   -- Change the brightness.
   for (TM1637_MAX_BRIGHTNESS + 1) using counter loop
      tm1637_set_brightness(counter)
      wait_short()
   end loop

   -- Now clear the display, that is all segments are off.
   tm1637_display_clear()
   wait_short()

   -- Set the dots on and off.
   for TM1637_WIDTH using counter loop
      tm1637_set_dot(TRUE, counter)
      wait_short()
      tm1637_set_dot(FALSE, counter)
    end loop

   -- Set an individual display and show its number.
   tm1637_display_clear()
   for TM1637_WIDTH using counter loop
      tm1637_set_display(tm1637_digit_to_segment(counter), counter)
      wait_short()
      tm1637_set_display(TM1637_SEGMENT_OFF,counter)
   end loop

   -- Write a hexdecimal nibble on the rightmost display.
   tm1637_display_clear()
   for 16 using counter loop
      tm1637_display_nibble_hex(counter,0)
      _usec_delay(200_000)
   end loop
   wait_short()
  
   -- Write a hexdecimal byte on the rightmost display.
   tm1637_display_clear()
   for 256 using counter loop
      tm1637_display_byte_hex(counter,0)
      _usec_delay(10_000)
   end loop
   wait_short()
   
   -- Write a hexdecimal word on the rightmost display.
   tm1637_display_clear()
   for 512 using word_counter loop
      tm1637_display_word_hex(word_counter,0)
   end loop
   wait_short()
   
   -- Show a counter in another way with leading zero's.
   tm1637_display_clear()
   for 2023 using big_pos_counter loop -- Up and until the year 2022 :-)
      tm1637_display_dword_dec(dword(big_pos_counter),0,TRUE)
   end loop
   wait_short()
   
   -- Show a negative counter in another way without leading zero's.
   tm1637_display_clear()
   for 1000 using big_pos_counter loop
      big_neg_counter = -big_pos_counter
      tm1637_display_sdword_dec(big_neg_counter,0,FALSE)
   end loop
   wait_short()

end loop

