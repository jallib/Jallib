-- --------------------------------------------------------------
-- Title: Example of using the stepper library
--
-- Author: Rob Hamerling, Copyright (c) 2016, all rights reserved.
--
-- Adapted by:
--
-- Compiler: 2.4q5
--
-- This file is part of jallib  (https://github.com/jallib/jallib)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
--
-- Description: Simple stepper motor example with 18f25k50
-- 
-- Sources:
--
-- Notes:
-- 
-- --------------------------------------------------------------

include 18f25k50

-- internal oscillator at 16 MHz (with 3xPLL -> 48 MHz)

pragma target clock    48_000_000               -- Fosc
--
pragma target OSC      INTOSC_NOCLKOUT          -- internal oscillator   
pragma target PLLSEL   PLL3X                    -- 3x PLL
pragma target PLLEN    ENABLED                  -- PLL
pragma target CPUDIV   P1                       -- Fosc divisor 1:1
pragma target WDT      DISABLED                 -- watchdog
pragma target XINST    DISABLED                 -- do not use extended instructionset
pragma target DEBUG    DISABLED                 -- no debugging
pragma target BROWNOUT DISABLED                 -- no brownout reset
pragma target FCMEN    DISABLED                 -- no clock monitoring
pragma target IESO     DISABLED                 -- no int/ext osc switching
pragma target LVP      ENABLED                  -- allow low voltage programming
pragma target MCLR     EXTERNAL                 -- external reset

OSCCON_IRCF = 0b111                             -- 16 MHz (with 3x PLL -> 48 MHz)
OSCCON_SCS = 0                                  -- select primary oscillator

enable_digital_io()                             -- make all pins digital I/O

include delay                                   -- library with delay procedures

alias  stepper_led            is pin_A0        -- led
alias  stepper_led_direction  is pin_A0_direction
stepper_led_direction         = OUTPUT 

alias  stepper_coils  is  portb_low             -- lower nibble of portb 
portB_low_direction = ALL_OUTPUT                -- all coil pins output      

const word stepper_steps_per_rotation = 48      -- steps per full rotation

include steppermotor                            -- stepper motor library 

-- ------- main program ------

forever loop

   stepper_led = ON        
   stepper_cw = TRUE                         -- clockwise
   stepper_fullstep = TRUE                   -- full steps
   stepper_accel(100,1500)                   -- accelaration rate 15 RPM/sec
   stepper_decel(100,1500)                   -- decelaration rate 15 RPM/sec
   stepper_led = ON
   stepper_run(600)                          -- 60 RPM
   delay_1s(6) 
  
   stepper_led = OFF 
   stepper_stop()                            -- decellerate to 0 RPM  
   while (stepper_actual_rpm() != 0) loop
      -- nop
   end loop   
   
   stepper_led = ON
   stepper_cw = FALSE                        -- reverse
   stepper_fullstep = FALSE                  -- half steps
   stepper_accel(100,1000)                   -- accelaration rate 10 RPM/sec
   stepper_decel(100,1000)                   -- decelaration rate 10 RPM/sec
   stepper_led = ON
   stepper_run(300)                          -- 30 RPM   
   delay_1s(6) 
   
   stepper_led = OFF
   stepper_stop()                            -- decellerate to 0 RPM  
   while (stepper_actual_rpm() != 0) loop
      -- nop
   end loop   
   
end loop
 


