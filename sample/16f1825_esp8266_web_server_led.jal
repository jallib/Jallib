-- -----------------------------------------------------------------------------
-- Title: ESP8266 web server led on/off sample program.
--
-- Author: Rob Jansen, Copyright (c) 2018..2019, all rights reserved.
--
-- Adapted-by:    
--
-- Compiler: 2.5r2
--
-- This file is part of jallib (https://github.com/jallib/jallib)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Sources: Inspired by https://github.com/bportaluri/WiFiEsp 
--
-- Description:
-- ------------
-- Start the ESP8266 as a webserver. The Web Page generated by the PIC will 
-- show show the status of the led and will provide two options to turn the 
-- led on the PIC on or off. The web browser must connect to the IP 
-- address of the ESP8266 which is obtained from the Access Point.
-- Make sure you have set the right SSID and password in 'MY_SSID' and 
-- 'MY_PASSWORD'.    
-- A second led will be on when then initialization was succesfull. 
--
-- Dependencies: -
--
-- Hardware info:
-- --------------
-- Important!. The ESP8266 uses a supply voltage of 3.3 Volt. Make sure that 
--             your PIC also runs at that supply voltage or make use of a level 
--             shifter for connecting the USART pins from the PIC to the ESP8266.
--
-- PIC connections (14 pin DIP):
-- *) TX pin (6) of the USART of the PIC to the RX pin of the ESP8266 module.
-- *) RX pin (5) of the USART of the PIC to the TX pin of the ESP8266 module.
-- *) The led to indicate that the ESP module is initialized via a 330 Ohm
--    resistor to pin 9 of the PIC. Led is active High.
-- *) The led that is controlled via Telniet via a 330 Ohm resistor to 
--    pin 8 of the PIC. Led is active High.
--
-- Connect the ESP8266 module as follows:
-- *) RST pin to 3.3 V
-- *) EN pin with a resistor of 1 kOhm to the 3.3 V 
-- *) GPIO15 pin with a resistor of 1 kOhn to ground
--
-- ------------------------------- Pragmas -------------------------------------
-- Some compiler pragmas for optimizations
pragma warn all no              
pragma opt variable_reduce yes   

include 16f1825                 

-- This program uses the internal oscillator at 32 MHz.
pragma target clock    32_000_000  -- oscillator frequency
--
-- Configuration memory settings (fuses). These are only a selection, sufficient
-- for this program.
pragma target OSC      INTOSC_NOCLKOUT -- Internal Clock
pragma target PLLEN    ENABLED         -- PLL on to get 32 MHz
pragma target WDT      DISABLED        -- No Watchdog
pragma target PWRTE    ENABLED         -- Power up timer enabled
pragma target BROWNOUT DISABLED        -- No brownout reset
pragma target FCMEN    DISABLED        -- No clock monitoring
pragma target IESO     DISABLED        -- int/ext osc. switch
pragma target LVP      ENABLED         -- No low voltage programming
pragma target MCLR     INTERNAL        -- Reset internal
-- Set the internal clock frequency to 32 MHz.
OSCCON_IRCF   = 0b1110                 -- Set 32 MHz (uses 8 MHz source)
OSCCON_SCS    = 0b00                   -- Clock determined by FOSC (32 MHz)

-- Enable weak pull up for all pins.
WPUA = 0b0011_1111       
WPUC = 0b0011_1111       
OPTION_REG_WPUEN = FALSE -- Enable Weak Pull-Up

enable_digital_io()

-- -------------------------------- Pins ---------------------------------------
-- Pin declarations for the USART connection to the ESP8266 module.
alias pin_RX_direction  is pin_C5_direction  -- Pin 5 for 14 pin DIP
alias pin_TX_direction  is pin_C4_direction  -- Pin 6 for 14 pin DIP.

alias connect_led is pin_C1                  -- Pin 9 for 14 pin DIP.
pin_C1_direction = output                    
alias control_led is pin_C2                  -- Pin 8 for 14 pin DIP.
pin_C2_direction = output                    

-- ------------------------- Constant declarations -----------------------------
-- The receive buffer must be large enough to hold one simple http request.
const ESP8266_MAX_RECEIVE_BUFFER = 600    

const byte MY_SSID[]     = "Your_ID"
const byte MY_PASSWORD[] = "Your_Password"
const byte MY_PORT[]     = "80" -- Port 80 is used for HTTP TCP requests.

const bit LED_ON  = TRUE
const bit LED_OFF = FALSE

-- -------------------------- Includes go here --- -----------------------------
include esp8266

-- ------------------------- Variable declarations -----------------------------
var byte connection 
var bit led_value
var bit all_ok

-- ------------------------- Functions and Procedures --------------------------
-- Parse the data in the ESP8266 receive buffer and return true if the 
-- given token if found.
function parse_response(byte in token[]) return bit is
   var word buffer_index
   var byte token_index
   
   -- Start token given, look for start token.
   token_index = 0
   buffer_index = 0
   while (token_index < count(token)) & (buffer_index < esp8266_bytes_received) loop
      if (esp8266_receive_buffer[buffer_index] == token[token_index]) then 
         token_index = token_index + 1
      else
         -- No match (yet), try again.
         token_index = 0
      end if  
      buffer_index = buffer_index + 1
   end loop
   return (token_index == count(token))
end function

-- Send the http response. The connection will be closed after completion of the 
-- response. We ignore the return value of the esp8266_send_data funtion.
procedure send_http_response() is
   
   -- Not concatenated the individual commands would be as follows:
   -- esp8266_send_data("HTTP/1.1 200 OK\r\n")
   -- esp8266_send_data("Content-Type: text/html\r\n")
   -- esp8266_send_data("Connection: close\r\n")  
   -- esp8266_send_data("\r\n") 
   -- esp8266_send_data("<html>\r\n")
   -- esp8266_send_data("The LED is ")
   -- if (led_value == LED_ON) then
   --    esp8266_send_data("On")  
   -- else
   --    esp8266_send_data("Off")
   -- end if 
   -- esp8266_send_data("<br>\r\n")
   -- esp8266_send_data("<br>\r\n")
   -- esp8266_send_data("Click <a href=\"/H\">here</a> turn the LED on<br>\r\n")
   -- esp8266_send_data("Click <a href=\"/L\">here</a> turn the LED off<br>\r\n")
   -- esp8266_send_data("</html>\r\n")      
   
   -- However, we concatenate some strings as to speed up the web response.
   esp8266_send_data("HTTP/1.1 200 OK\r\nContent-Type: text/html\r\nHTTP/1.1 200 OK\r\nConnection: close\r\n\r\n")
   if (led_value == LED_ON) then
      esp8266_send_data("<html>\r\nThe LED is On<br>\r\n<br>\r\nClick <a href=\"/H\">here</a> turn the LED on<br>\r\nClick <a href=\"/L\">here</a> turn the LED off<br>\r\n</html>\r\n")  
   else
      esp8266_send_data("<html>\r\nThe LED is Off<br>\r\n<br>\r\nClick <a href=\"/H\">here</a> turn the LED on<br>\r\nClick <a href=\"/L\">here</a> turn the LED off<br>\r\n</html>\r\n")
   end if 
   esp8266_close_connection()
end procedure


-- ------------------------------- Main Program  ------------------------------- 
_usec_delay(1_000_000) -- Gives ESP module also time to start up.

connect_led = LED_OFF
control_led = LED_OFF
led_value = LED_OFF

-- Initialize the device and connect to the Access Point until succes.
all_ok = FALSE
while !all_ok loop
   if esp8266_init() then
      -- Succes, connect to the Access Point.
      while (esp8266_get_connection_status() != ESP8266_CONNECTED) loop
         if esp8266_connect_to_ap(MY_SSID, MY_PASSWORD) then
            all_ok = TRUE
         end if       
      end loop
   end if 
end loop

all_ok = FALSE
-- Start the webserver with a timeout of 10 minutes (600 seconds).
while !all_ok loop
   all_ok = esp8266_server_start(MY_PORT)
   all_ok = all_ok & esp8266_set_server_timeout("600")
end loop

connect_led = LED_ON -- Module initialized.

forever loop
   control_led = led_value
   if esp8266_data_available() then
      if esp8266_get_data(connection) then
         -- We need to check what this is before taking action. It is either
         -- a request for the web pages or a response on clicking a link on
         -- the generated web page.
         -- A HTTP request has the following format:
         -- *) A Request-line
         -- *) Zero or more header (General|Request|Entity) fields followed by CRLF
         -- *) An empty line (i.e., a line with nothing preceding the CRLF) 
         --    indicating the end of the header fields
         -- *) Optionally a message-body
         
         -- Make sure that the response we will send goes to the this connection.
         esp8266_set_connection(connection)
         
         -- Check to see if the client request was "GET /H" or "GET /L"
         if parse_response("GET /H") then
            led_value = LED_ON
            send_http_response()  
         elsif parse_response("GET /L") then
            led_value = LED_OFF
            send_http_response()  
         elsif parse_response("\r\n\r\n") then
            -- We interpret 2 new-lines as a http request.
            send_http_response()  
         end if
      end if
   end if
end loop



