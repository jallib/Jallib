-- --------------------------------------------------------------------------------
-- Title: ADC hardware, built-in PIC ADC module
-- Author: Stef Mientki, Copyright (c) 2002..2021, all rights reserved.
-- Adapted-by: Sebastien Lelong, Rob Hamerling, Rob Jansen
-- Compiler: 2.5r5
--
-- This file is part of jallib (https://github.com/jallib/jallib)
-- Released under the ZLIB license (http://www.opensource.org/licenses/zlib-license.html)
--
-- Description: Library to handle ADC peripheral, supporting low (8-bits) and high 
--              (16-bits) resolution (whatever the PIC supports). Since the differences
--              between the various ADC features are too different for various PICs, this
--              library only provides some basic ADC features.
--              The user has to do the following in the main program using the datasheet
--              of the PIC that is being used:
--              1) Set the used ADC pin(s) as analog input
--              2) Set the refernce voltage for the ADC (default is often VDD)
--              3) Set the ADC clock according to the datasheet and the target clock
--                 being used by the main program (FRC is safe to use)
-- 
-- For more information:
-- -) See http://justanotherlanguage.org/content/jallib/tutorials/tutorial_adc_intro
--
-- Some constants can be overruled by the main program:
-- - ADC_TEMP:    Careful, computation related to temperature very approximative
-- - ADC_RSOURCE: Input resistance on ADC input channel
-- - ADC_MAX_TAD: Maximum conversion time.     
--
-- Notes: This is a heavy refactoring/rewriting of original library of Stef Mientki.
--        Deprecated functions have been removed.
--
-- --------------------------------------------------------------------------------

-- By default ADC_RSOURCE is the maximum recommended value in the datasheets
if defined(ADC_RSOURCE) == false then              -- not defined by user
   const word ADC_RSOURCE = 10_000                 -- default: 10K
end if

if defined(ADC_TEMP) == false then                 -- not defined by user
   const byte ADC_TEMP = 25                        -- default: 25 degrees C
end if

-- global variables

-- Time to have the analog signal converted to a digital value.
-- Value is calculated in _adc_init_acquisition_delay() and
-- used in _adc_read().
var byte  adc_conversion_delay                     -- set in procedure
                                                   -- _adc_init_acquisition_delay()

-- constants for justification selection
const bit _ADC_JUSTIFY_LEFT  = 0                   -- internally used
const bit _ADC_JUSTIFY_RIGHT = 1                   -- constants

-- Set values ADC_TAD_MAX when not user specified.
if defined(ADC_MAX_TAD) == false then
   if target_cpu == PIC_16 then                 -- 18F family
      const ADC_MAX_TAD = 25                    -- 25 usec maximum
   else                                         -- non-18F
      const ADC_MAX_TAD = 6                     -- 6 usec maximum
   end if
end if

-- ------ Some aliases to deal with different types of PICs --------


-- --------------------------------------------------------------
-- Title:     Analog to Digital conversion of the selected channel.
-- Arguments: - channel number (byte)
--            - justification (bit 0=left, 1=right)
-- Returns:   word with ADC value
-- Notes:     - When PIC supports high resolution (more than 8 bits)
--              all bits of the ADC conversion are stored straight over
--              from ADRESH and ADRESL.  When PIC supports only 8-bits
--              resolution only the contents of ADRES are stored.
--            - With right justification the result is immediately
--              usable as binary value.
--            - Left justification allows easy selection of the 8 most
--              significant bits of the result, regardless which
--              resolution the PIC supports (can be 8-,10 or 12-bits).
-- --------------------------------------------------------------
function _adc_read(byte in adc_chan, bit in justify) return word is

   var word adc_word                               -- return value
   var byte adc_byte[2] at adc_word                -- byte array overlay

   -- Channel selection for different PICs.
   if defined(ADPCH) then
      ADPCH = adc_chan
   else
      -- Use ADCON0 as default. Note: It has to be done like this since
      -- the PIC16F767 only has a pseudo variable for ADCON0_CHS which
      -- is not detected when using 'if defined()'.
      ADCON0_CHS = adc_chan
   end if 

   -- Left or right justify selection. The ADFM bits can be in any of the
   -- ADCONx registers or may not exist at all (so there is no 'else').
   if defined(ADCON0_ADFM) then                    
      ADCON0_ADFM = justify                          
   elsif defined(ADCON1_ADFM) then                  
      ADCON1_ADFM = justify                         
   elsif defined(ADCON2_ADFM) then        
      ADCON2_ADFM = justify                  
   elsif defined(ADCON0_ADFRM0) then       
      ADCON0_ADFRM0 = justify       
   end if

   ADCON0_ADON = TRUE                              -- turn on ADC module
   for adc_conversion_delay loop
      _usec_delay(10)                              -- wait acquisition time
   end loop
   ADCON0_GO = TRUE                                -- start conversion
   while ADCON0_GO == TRUE loop                    -- wait until conversion completed
      -- Empty loop.
   end loop
   -- 2 cases:
   --   - PIC has high resolution ADC: need to read ADRESH and ADRESL
   --   - PIC has only a 8-bits ADC: need to read only ADRES
   if defined(ADRESH) then                         -- PIC has high resolution ADC
      adc_byte[1] = ADRESH                         -- copy high order bits
      adc_byte[0] = ADRESL                         -- copy low order bits
   else                                            -- (PIC has no high resolution ADC)
      if justify == _ADC_JUSTIFY_LEFT then         -- left justified
         adc_byte[1] = byte(ADRES)                 -- result in high order byte
         adc_byte[0] = 0b0000_0000                 -- zero low order byte
      else                                         -- (right justified)
         adc_byte[1] = 0b0000_0000                 -- zero high order byte
         adc_byte[0] = byte(ADRES)                 -- result in low order byte
      end if
   end if
   -- Honor 2 * max Tad time. Note the value is not very accurate, but safe.
   _usec_delay(2 * ADC_MAX_TAD)
   ADCON0_ADON = false                             -- turn off ADC module
   return adc_word                                 -- back to caller
end function


-- --------------------------------------------------------------
-- Title:     Analog to digital conversion returning an 8-bits value
-- Arguments: Channel number
-- Returns:   8-bits ADC value
-- Notes:     - Uses left justification.
--            - Returns the 8 most significant bits of the result of ADC
-- --------------------------------------------------------------
function adc_read_low_res(byte in adc_chan) return byte is
   return byte(_adc_read(adc_chan, _ADC_JUSTIFY_LEFT) >> 8)    -- do conversion, return MSB
end function


-- --------------------------------------------------------------
-- Title:     Analog to digital conversion returning a 16-bits value
-- Arguments: channel number
-- Returns:   16-bits ADC value
-- Notes:     - Uses right justification
--            - Returns a 16-bits value of the result of ADC, of which the
--              range is dependent of the supported ADC resolution of the PIC
--              (can be a 8-, 10-, 12-bits value).
--            - When the PIC supports only 8-bits ADC resolution the
--              result is the same as of adc_read_low_res().
-- --------------------------------------------------------------
function adc_read_high_res(byte in adc_chan) return word is
   return _adc_read(adc_chan, _ADC_JUSTIFY_RIGHT)              -- do conversion, return word
end function


-- --------------------------------------------------------------
-- Title:     Calculation of acquisition delay
-- Arguments: (none)
-- Returns:   nothing
-- Notes:     result is stored in global variable _adc_conversion_delay
-- --------------------------------------------------------------
procedure _adc_init_acquisition_delay() is
   pragma inline
   -- Seb: holy datasheet says:
   --    Tc = Chold * (Ric + Rss + Rs) * ln(0.0004885)
   -- I don't know Ric and Rss are, but Rs is the impedance for analog sources
   -- For some PICs, this impedance is max. 10K (16f877), for others it's 2.5K (16f88)
   -- Interestingly, 16f88 has 2.5K as max impedance, but the formula is still the same
   -- and uses 10K. Anyway...
   -- Using this 10K as max, Tc = 16.47usecs
   --
   -- Stef says he adds 10usecs just in case, and consider a 15usecs delays, and also consider Rs !
   -- Old code:
   --       -- calculate AD-conversion time in 10 usec units
   --       -- an extra 10 usec is added to ensure the minimum acquistion time
   --       const adc_conversion_delay = (10 + 15 + (adc_hardware_rsource / 1_000  )) / 10
   --       _adc_acquisition_time = adc_conversion_delay
   --
   -- I think there's too much here, right ?
   --   * either you consider 15usecs + 10usecs as "16.47usecs + some more just in case"
   --   * or you don't consider the max Rs, but the actual specified value and add some "10usecs just in case"
   -- Mmmh... Don't know what to do :)
   --
   -- Tacq        = Tamp + Tc + Tcoff
   -- Tc(secs)    = Chold * (Ric + Rss + Rs) * ln(0.0004885)
   -- Tc(usecs)   = (-120 * (1 + 7 + Rs) * -7.63) / 1000
   -- Tc(usecs)   = (915 * (8 + Rs)) / 1000
   -- Tacq(usecs) = 2 + Tc(usecs) + (0.05usecs * T)
   -- So Tc, expressed in usecs is:
   -- (add 1 so we have value at ceil)
   const byte adc_tc = ((915 * (8 + (ADC_RSOURCE/1000))) / 1000) + 1
  
   -- Let's be generous... This is very, very approximative...
   -- Even if ADC_TEMP = 0, it should be ok at 25 C to 30 C
   -- It'll give 1 usec instead of 1.25 to 1.5 usecs, but since there
   -- (I even wonder if it makes sense to compute this...)
   const byte adc_tcoff = ((ADC_TEMP * 5) / 100) + 1

   adc_conversion_delay = 2 + adc_tc + adc_tcoff   -- Tamp seems to be a constant: 2usecs

end procedure


-- --------------------------------------------------------------
-- Tilte:     Initialise ADC module and default parameters
-- Arguments: (none)
-- Returns:   nothing
-- --------------------------------------------------------------
-- Initializes the PIC AD-converter, calculate aquisition time.
procedure adc_init() is
   _adc_init_acquisition_delay()
end procedure

-- --------------------------------------------------------------

